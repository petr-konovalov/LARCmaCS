# Copyright 2019 Irina Khonakhbeeva

# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at

# http://www.apache.org/licenses/LICENSE-2.0

# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

#Check if 'protoc' is available
PROTOC_VER = $$system($${PROTOC_DIR}protoc --version)
equals(PROTOC_VER,"") {
  error(Is the google protocol buffer compiler 'protoc' installed?)
}
#Generating protocol buffer classes from .proto files
protobuf_decl.name = protobuf headers
protobuf_decl.input = PROTOS
protobuf_decl.output = $${PROTO_GENERATED_DIR}/${QMAKE_FILE_BASE}.pb.h
protobuf_decl.CONFIG += explicit_dependencies no_link target_predeps
protobuf_decl.depends = ${QMAKE_FILE_IN}
protobuf_decl.commands = $$shell_path($${PROTOC_DIR}protoc) --cpp_out=$$shell_path$${PROTO_GENERATED_DIR} --proto_path=${QMAKE_FILE_IN_PATH} ${QMAKE_FILE_BASE}.proto
#protobuf_decl.commands = $${PROTOC_DIR}protoc --cpp_out=$${PROTO_GENERATED_DIR} --proto_path=$${PROTO_GENERATED_DIR}/.. ${QMAKE_FILE_BASE}.proto

protobuf_decl.variable_out = GENERATED_FILES
QMAKE_EXTRA_COMPILERS += protobuf_decl

protobuf_impl.name = protobuf sources
protobuf_impl.input = PROTOS
protobuf_impl.output = $${PROTO_GENERATED_DIR}/${QMAKE_FILE_BASE}.pb.cc
protobuf_impl.depends = $${PROTO_GENERATED_DIR}/${QMAKE_FILE_BASE}.pb.h
protobuf_impl.dependency_type = TYPE_C
protobuf_decl.CONFIG += explicit_dependencies no_link target_predeps
protobuf_impl.commands = $$escape_expand(\n)
protobuf_impl.variable_out = GENERATED_SOURCES
QMAKE_EXTRA_COMPILERS += protobuf_impl

